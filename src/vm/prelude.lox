class Iter {
    count() {
        var c = 0;
        for(var i = this.next(); i != nil; i = this.next()) {
            c = c + 1;
        }

        return c;
    }

    sum() {
        var ret = 0;
        for(var i = this.next(); i != nil; i = this.next()) {
            ret = ret + i;
        }
        return ret;
    }

    windows(n) {
        return WindowIter(this, n);
    }

    map(f) {
        return MapIter(this, f);
    }

    filter(pred) {
        return FilterIter(this, pred);
    }
}

class MapIter < Iter {
    init(other, f) {
        this.iter = other;
        this.f = f;
    }

    next() {
        var next = this.iter.next();
        if (next == nil) {
            return nil;
        }
        return this.f(next);
    }
}

class FilterIter < Iter {
    init(other, pred) {
        this.iter = other;
        this.pred = pred;
    }

    next() {
        var ret = nil;

        while (ret == nil) {
            var next = this.iter.next();
            if (next == nil) {
                return nil;
            }
            if (this.pred(next)) {
                ret = next;
            }
        }

        return ret;
    }
}

class WindowIter < Iter {
    init(other, n) {
        this.iter = other;
        this.n = n;
        this.last = nil;
    }

    next() {
        if (!this.last) {
            var ret = vec();
            for (var i = 0; i < this.n; i = i + 1) {
                var tmp = this.iter.next();
                if (tmp == nil) {
                    return nil;
                }
                ret.push(tmp);
            }
            this.last = ret;
            return ret;
        }
        this.last.rotateLeft(1);
        var tmp = this.iter.next();
        if (tmp == nil) {
            return nil;
        }
        this.last.set(this.n-1, tmp);
        return this.last;
    }
}

class VecIter < Iter {
    init(vec) {
        this.vec = vec;
        this.pos = 0;
    }

    next() {
        if (this.pos < this.vec.len()) {
            var ret = this.vec[this.pos];
            this.pos = this.pos + 1;
            return ret;
        }
        return nil;
    }
}

